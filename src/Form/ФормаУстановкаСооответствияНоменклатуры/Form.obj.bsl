

// функции для совместимости кода 
Функция сбисПолучитьФорму(ИмяФормы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат ПолучитьФорму("ВнешняяОбработка.СБИС.Форма."+ИмяФормы);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы);
КонецФункции

Функция сбисЭлементФормы(Форма,ИмяЭлемента)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Форма.Элементы.Найти(ИмяЭлемента);
	КонецЕсли;
	Возврат Форма.ЭлементыФормы.Найти(ИмяЭлемента);
КонецФункции

Функция Показать(ПараметрыФормы) Экспорт	
	
	Если ПараметрыФормы <> Неопределено Тогда
		Наименование = ПараметрыФормы.Наименование; 
	КонецЕсли;
	
	СписокНоменклатуры.Очистить();
	
	Если Не ЭтаФорма.Открыта() Тогда
		ЭтаФорма.Открыть();
	КонецЕсли;	
КонецФункции	

Процедура СписокНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Колонка.Имя = "Пересчет" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекущаяСтрока = ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные;
		
		ФормаУстановкаСооответствия = сбисПолучитьФорму("ФормаПересчетаЕдиницEDI_В_1С");				
						
		ФормаУстановкаСооответствия.ЕдиницаEDI			= ТекущаяСтрока.ЕдиницаEDI;
		ФормаУстановкаСооответствия.ЕдиницаИзмерения	= ТекущаяСтрока.ЕдиницаИзмерения;
		ФормаУстановкаСооответствия.КоэффициентEDIВ1С	= ТекущаяСтрока.КоэффициентEDIВ1С;
				
		Структура = ФормаУстановкаСооответствия.ОткрытьМодально();
		
		Если ТипЗнч(Структура) = Тип("Структура") Тогда
			
			ТекущаяСтрока.КоэффициентEDIВ1С	= Структура.КоэффициентEDIВ1С;
			ОбработчикПослеВведенияПересчета();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикПослеВведенияПересчета()
	
	УстановитьТекстПересчета();
	
КонецПроцедуры        

Процедура КоманднаяПанельСоответствующейНоменклатурыУдалить(Команда)
	
	Если ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные<>Неопределено Тогда 
		СписокНоменклатуры.Удалить(ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные); 
		ПроверитьНаличиеОсновнойНоменклатуры();
	Иначе
		Сообщить("Выберите строку соответствия для удаления");
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокНоменклатурыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущаяСтрока = ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные;
	ТекущаяСтрока.КоэффициентEDIВ1С	= 1;
	УстановитьТекстПересчета();
КонецПроцедуры

Процедура СписокНоменклатурыЕдиницаEDIПриИзменении(Элемент)
	ТекущаяСтрока = ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные; 
	ТекущаяСтрока.КоэффициентEDIВ1С	= 1;
	УстановитьТекстПересчета();
КонецПроцедуры

Процедура УстановитьТекстПересчета() 
	ВыбраннаяСтрока = ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ВыбраннаяСтрока.ЕдиницаEDI) И ЗначениеЗаполнено(ВыбраннаяСтрока.ЕдиницаИзмерения) Тогда
		ВыбраннаяСтрока.Пересчет = "1 " + Строка(ВыбраннаяСтрока.ЕдиницаEDI) + " равен " + Строка(ВыбраннаяСтрока.КоэффициентEDIВ1С) + " " + Строка(ВыбраннаяСтрока.ЕдиницаИзмерения);	
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельСоответствующейНоменклатурыУстановитьОсновным(Кнопка)

	СписокНоменклатуры.ЗаполнитьЗначения(Ложь,"Основной");	
	
	ТекСтрока = ЭлементыФормы.СписокНоменклатуры.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		ТекСтрока.Основной = Истина;
		
	КонецЕсли;
	
	ПроверитьНаличиеОсновнойНоменклатуры();	
	
КонецПроцедуры

Процедура ПроверитьНаличиеОсновнойНоменклатуры()
	
	Если СписокНоменклатуры.Количество()>0 Тогда
		
		НайденнаяСтрока = СписокНоменклатуры.Найти(Истина,"Основной");
		
		Если НайденнаяСтрока = Неопределено Тогда
			
			СписокНоменклатуры[0].Основной = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокНоменклатурыПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		
		Если ДанныеСтроки.Основной Тогда
			
			ОформлениеСтроки.Шрифт = Новый Шрифт(ОформлениеСтроки.Шрифт,,,Истина);
			
		КонецЕсли;
		                                                                  		
	КонецЦикла;

КонецПроцедуры
